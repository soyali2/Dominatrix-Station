/datum/round_event_control/pirates
	name = "Space Pirates"
	typepath = /datum/round_event/pirates
	weight = 8
	max_occurrences = 1
	min_players = 25 // порог от больших серверов резал разнообразие на типичных 25-35: гост-пул сужался до метеора
	// Было 45 мин: к этому времени кошелёк уже 2-3 раза выжжен ранней волной, и за день
	// логов 9766-9775 пираты не выпали ни разу. 30 мин - конкуренция с основной волной.
	earliest_start = 30 MINUTES
	category = EVENT_CATEGORY_INVASION
	severity = DIRECTOR_SEVERITY_GHOST // антаги из призраков - гост-пул, а не общий MAJOR
	cost = 10
	intensity = 15
	family = "pirates" // с рулсетом-двойником динамика (он запускает это же событие): не подряд
	required_round_type = list(ROUNDTYPE_DYNAMIC_TEAMBASED, ROUNDTYPE_DYNAMIC_HARD, ROUNDTYPE_DYNAMIC_MEDIUM) // как у рулсета-двойника: не экста и не лайт
	description = "The crew will either pay up, or face a pirate assault."

#define PIRATES_ROGUES "Rogues"
// #define PIRATES_SILVERSCALES "Silverscales"
// #define PIRATES_DUTCHMAN "Flying Dutchman"

/datum/round_event_control/pirates/preRunEvent(admin_window = TRUE)
	if(!SSmapping.empty_space && !length(SSmapping.levels_by_trait(ZTRAIT_SPACE_RUINS)) && !SSmapping.station_start)
		return EVENT_CANT_RUN

	return ..()

/datum/round_event/pirates
	var/pirates_spawned = FALSE
	var/spawn_timer_id

/datum/round_event/pirates/start()
	send_pirate_threat()

/datum/round_event/pirates/proc/send_pirate_threat()
	var/pirate_type = PIRATES_ROGUES //pick(PIRATES_ROGUES, PIRATES_SILVERSCALES, PIRATES_DUTCHMAN)
	var/datum/comm_message/threat_msg = new
	var/payoff = 0
	var/payoff_min = 25000 //documented this time
	var/ship_template
	var/ship_name = "Space Privateers Association"
	var/initial_send_time = world.time
	var/response_max_time = 5 MINUTES
	switch(pirate_type)
		if(PIRATES_ROGUES)
			ship_name = pick(strings(PIRATE_NAMES_FILE, "rogue_names"))

	priority_announce("Входящая подпространственная передача данных. Открыт защищенный канал связи на всех коммуникационных консолях.", "Предложение о Защите Сектора", SSstation.announcer.get_rand_report_sound(), has_important_message = TRUE)
	var/datum/bank_account/D = SSeconomy.get_dep_account(ACCOUNT_CAR)
	if(D)
		payoff = max(payoff_min, FLOOR(D.account_balance * 0.80, 1000))
	switch(pirate_type)
		if(PIRATES_ROGUES)
			ship_template = /datum/map_template/shuttle/pirate/default
			threat_msg.title = "Предложение о Защите Сектора"
			threat_msg.content = "Приветствуем вас с корабля [ship_name]. Ваш сектор нуждается в защите, заплатите нам [payoff] кредитов или на вас наверняка кто-то нападёт."
			threat_msg.possible_answers = list("Мы заплатим.","Мы заплатим, но на самом деле нет.")

	threat_msg.answer_callback = CALLBACK(src, PROC_REF(pirates_answered), threat_msg, payoff, ship_name, initial_send_time, response_max_time, ship_template)
	SScommunications.send_message(threat_msg,unique = TRUE)
	spawn_timer_id = addtimer(CALLBACK(src, PROC_REF(spawn_pirates), threat_msg, ship_template), response_max_time, TIMER_STOPPABLE)

/datum/round_event/pirates/proc/pirates_answered(datum/comm_message/threat_msg, payoff, ship_name, initial_send_time, response_max_time, ship_template)
	if(world.time > initial_send_time + response_max_time)
		priority_announce("Слишком поздно умолять о пощаде!", ship_name, 'modular_bluemoon/phenyamomota/sound/announcer/pirate_nopeacedecision.ogg', "Priority")
		spawn_pirates(threat_msg, ship_template, TRUE)
		return
	if(threat_msg && threat_msg.answered == 1)
		var/datum/bank_account/D = SSeconomy.get_dep_account(ACCOUNT_CAR)
		if(D && D.adjust_money(-payoff))
			priority_announce("Спасибо за кредиты, сухопутные крысы!", ship_name, 'modular_bluemoon/phenyamomota/sound/announcer/pirate_yespeacedecision.ogg', "Priority")
			SSdirector.complete_deferred_action_without_roles(control, "угроза снята выкупом; назначено ролей: 0")
			resolve_threat_peacefully()
			return
		priority_announce("Пытаешься нас обмануть? Ты пожалеешь об этом!", ship_name, 'modular_bluemoon/phenyamomota/sound/announcer/pirate_nopeacedecision.ogg', "Priority")
		spawn_pirates(threat_msg, ship_template, TRUE)
		return
	else
		priority_announce("Пытаешься нас обмануть? Ты пожалеешь об этом!", ship_name, 'modular_bluemoon/phenyamomota/sound/announcer/pirate_nopeacedecision.ogg', "Priority")
		spawn_pirates(threat_msg, ship_template, TRUE)

/datum/round_event/pirates/proc/get_spawn_z()
	if(SSmapping.empty_space)
		return SSmapping.empty_space.z_value
	var/list/space_zlevels = SSmapping.levels_by_trait(ZTRAIT_SPACE_RUINS)
	if(length(space_zlevels))
		return pick(space_zlevels)
	return SSmapping.station_start

/datum/round_event/pirates/proc/resolve_threat_peacefully()
	pirates_spawned = TRUE
	if(spawn_timer_id)
		deltimer(spawn_timer_id)
		spawn_timer_id = null

/// Спавн не состоялся: возвращаем директору бюджет и паузы, чтобы он подобрал замену.
/// Провал терминален - иначе оставшийся таймер или ответ станции зашли бы сюда второй раз
/// и вернули бы бюджет дважды.
/datum/round_event/pirates/proc/fail_spawn(reason)
	pirates_spawned = TRUE
	if(spawn_timer_id)
		deltimer(spawn_timer_id)
		spawn_timer_id = null
	message_admins("Space Pirates event failed: [reason]")
	if(!control)
		return
	// Бюджет тратился только на естественный запуск через бит (админ-форс идёт мимо кошельков).
	SSdirector.note_failed_action(control, refund_budget = triggered_randomly, retry_replacement = triggered_randomly)
	SSdirector.director_log_beat(SSdirector.collect_signals(), control, DIRECTOR_BEAT_FAILED,
		detail = "[reason]; [triggered_randomly ? "бюджет и паузы возвращены, запрошена замена" : "ручной запуск, бюджет не списывался; паузы возвращены"]")

/datum/round_event/pirates/proc/spawn_pirates(datum/comm_message/threat_msg, ship_template, skip_answer_check)
	if(pirates_spawned)
		return
	if(!skip_answer_check && threat_msg?.answered == 1)
		return
	if(!ship_template)
		fail_spawn("не задан шаблон корабля")
		return

	var/z = get_spawn_z()
	if(!z)
		fail_spawn("нет подходящего Z-уровня для корабля")
		return

	// Флаг ставится до загрузки: ship.load() спит (CHECK_TICK в парсере карты), и без него
	// сработавший за это время таймер или ответ станции загрузили бы второй корабль.
	pirates_spawned = TRUE
	if(spawn_timer_id)
		deltimer(spawn_timer_id)
		spawn_timer_id = null

	var/datum/map_template/shuttle/pirate/ship = new ship_template
	var/x = rand(TRANSITIONEDGE, world.maxx - TRANSITIONEDGE - ship.width)
	var/y = rand(TRANSITIONEDGE, world.maxy - TRANSITIONEDGE - ship.height)
	var/turf/T = locate(x,y,z)
	if(!T || !ship.load(T))
		fail_spawn("корабль не удалось загрузить на карту")
		return

	var/list/spawners_list = list()
	for(var/turf/A in ship.get_affected_turfs(T))
		for(var/obj/effect/mob_spawn/human/pirate/spawner in A)
			spawners_list += spawner

	var/list/candidates = pollGhostCandidates("Вы желаете стать пиратом?", ROLE_TRAITOR, minimum_required = spawners_list.len)
	var/list/spawned_pirates = list()
	var/spawner_count = length(spawners_list)
	var/intensity_share = spawner_count ? control.intensity / spawner_count : 0
	var/refund_share = triggered_randomly && spawner_count ? control.cost / spawner_count : 0

	for(var/obj/effect/mob_spawn/human/spawner in spawners_list)
		if(LAZYLEN(candidates))
			var/mob/our_candidate = pick_n_take(candidates)
			var/mob/living/spawned_pirate = spawner.create(our_candidate.ckey)
			if(spawned_pirate)
				spawned_pirates += spawned_pirate
			notify_ghosts("The pirate ship has an object of interest: [our_candidate]!", source=our_candidate, action=NOTIFY_ORBIT, header="Something's Interesting!")
		else
			spawner.director_source_action = control
			spawner.director_intensity = intensity_share
			spawner.director_refund_cost = refund_share
			notify_ghosts("The pirate ship has an object of interest: [spawner]!", source=spawner, action=NOTIFY_ORBIT, header="Something's Interesting!")
	if(length(spawned_pirates))
		var/spawned_fraction = length(spawned_pirates) / max(1, spawner_count)
		SSdirector.track_ghost_role_spawn(
			control,
			spawned_pirates,
			budget_backed = triggered_randomly,
			intensity_override = control.intensity * spawned_fraction,
			refund_cost_override = triggered_randomly ? control.cost * spawned_fraction : 0,
		)
	else
		SSdirector.director_log_beat(SSdirector.collect_signals(), control, DIRECTOR_BEAT_EXECUTED,
			detail = "корабль создан; сразу назначено ролей: 0, свободные спавнеры оставлены призракам")

	priority_announce("В секторе обнаружен вооруженный корабль.", "Отдел ССО ПАКТа Синих Лун", 'modular_bluemoon/phenyamomota/sound/announcer/pirate_incoming.ogg')

//Shuttle equipment

/obj/machinery/shuttle_scrambler
	name = "Data Siphon"
	desc = "This heap of machinery steals credits and data from unprotected systems and locks down cargo shuttles."
	icon = 'icons/obj/machines/dominator.dmi'
	icon_state = "dominator"
	density = TRUE
	var/active = FALSE
	var/credits_stored = 0
	var/siphon_per_tick = 18

/obj/machinery/shuttle_scrambler/Initialize(mapload)
	. = ..()
	update_icon()

/obj/machinery/shuttle_scrambler/process()
	if(active)
		if(is_station_level(z))
			var/datum/bank_account/D = SSeconomy.get_dep_account(ACCOUNT_CAR)
			if(D)
				var/siphoned = min(D.account_balance,siphon_per_tick)
				D.adjust_money(-siphoned)
				credits_stored += siphoned
			interrupt_research()
		else
			return
	else
		STOP_PROCESSING(SSobj,src)

/obj/machinery/shuttle_scrambler/proc/toggle_on(mob/user)
	SSshuttle.registerTradeBlockade(src)
	AddComponent(/datum/component/gps, "Nautical Signal")
	active = TRUE
	to_chat(user,"<span class='notice'>Вы [active ? "включаете":"выключаете"] [src].</span>")
	to_chat(user,"<span class='warning'>Сигнал устройства теперь может быть отслежен через GPS.</span>")
	START_PROCESSING(SSobj,src)

/obj/machinery/shuttle_scrambler/interact(mob/user)
	if(!active)
		if(alert(user, "Включение устройства позволит отследить шаттл с помощью GPS. Вы уверены?", "Scrambler", "Да", "Нет") == "Нет")
			return
		if(active || !user.canUseTopic(src, BE_CLOSE))
			return
		toggle_on(user)
		update_icon()
		send_notification()
	else
		dump_loot(user)

//interrupt_research
/obj/machinery/shuttle_scrambler/proc/interrupt_research()
	for(var/obj/machinery/rnd/server/S in GLOB.machines)
		if(S.machine_stat & (NOPOWER|BROKEN))
			continue
		S.emp_act(80)
		new /obj/effect/temp_visual/emp(get_turf(S))

/obj/machinery/shuttle_scrambler/proc/dump_loot(mob/user)
	if(credits_stored) // Prevents spamming empty holochips
		new /obj/item/holochip(drop_location(), credits_stored)
		to_chat(user,"<span class='notice'>You retrieve the siphoned credits!</span>")
		credits_stored = 0
	else
		to_chat(user,"<span class='notice'>There's nothing to withdraw.</span>")

/obj/machinery/shuttle_scrambler/proc/send_notification()
	priority_announce("Зарегистрирована кража данных, источник зафиксирован на локальных устройствах GPS.")

/obj/machinery/shuttle_scrambler/proc/toggle_off(mob/user)
	SSshuttle.clearTradeBlockade(src)
	active = FALSE
	STOP_PROCESSING(SSobj,src)

/obj/machinery/shuttle_scrambler/update_icon_state()
	icon_state = active ? "dominator-blue" : "dominator"
	return ..()

/obj/machinery/shuttle_scrambler/Destroy()
	toggle_off()
	return ..()

/obj/machinery/computer/shuttle/pirate
	name = "Pirate shuttle console"
	shuttleId = "pirateship"
	icon_screen = "syndishuttle"
	icon_keyboard = "syndie_key"
	light_color = LIGHT_COLOR_RED
	req_access = list(ACCESS_SYNDICATE)
	possible_destinations = "pirateship_away;pirateship_home;pirateship_custom"

/obj/machinery/computer/camera_advanced/shuttle_docker/syndicate/pirate
	name = "Pirate Shuttle Navigation Computer"
	desc = "Used to designate a precise transit location for the pirate shuttle."
	shuttleId = "pirateship"
	lock_override = CAMERA_LOCK_STATION
	shuttlePortId = "pirateship_custom"
	x_offset = 11
	y_offset = 1
	see_hidden = FALSE

/obj/docking_port/mobile/pirate
	name = "pirate shuttle"
	shuttle_id = "pirateship"
	rechargeTime = 3 MINUTES

/obj/machinery/suit_storage_unit/pirate
	helmet_type = /obj/item/clothing/head/helmet/space/pirate/bandana/eva
	suit_type = /obj/item/clothing/suit/space/pirate
	mask_type = /obj/item/clothing/mask/gas/glass
	storage_type = /obj/item/tank/jetpack/oxygen/harness

/obj/machinery/suit_storage_unit/pirate/captain
	helmet_type = /obj/item/clothing/head/helmet/space/pirate/eva
	suit_type = /obj/item/clothing/suit/space/pirate
	mask_type = /obj/item/clothing/mask/gas/glass
	storage_type = /obj/item/tank/jetpack/oxygen/harness

/obj/machinery/loot_locator
	name = "Booty Locator"
	desc = "This sophisticated machine scans the nearby space for items of value."
	icon = 'icons/obj/machines/research.dmi'
	icon_state = "tdoppler"
	density = TRUE
	var/cooldown = 300
	var/next_use = 0

/obj/machinery/loot_locator/interact(mob/user)
	if(world.time <= next_use)
		to_chat(user,"<span class='warning'>[src] is recharging.</span>")
		return
	next_use = world.time + cooldown
	var/atom/movable/AM = find_random_loot()
	if(!AM)
		say("No valuables located. Try again later.")
	else
		say("Located: [AM.name] at [get_area_name(AM)]")

/obj/machinery/loot_locator/proc/find_random_loot()
	if(!GLOB.exports_list.len)
		setupExports()
	var/list/possible_loot = list()
	for(var/datum/export/pirate/E in GLOB.exports_list)
		possible_loot += E
	var/datum/export/pirate/P
	var/atom/movable/AM
	while(!AM && possible_loot.len)
		P = pick_n_take(possible_loot)
		AM = P.find_loot()
	return AM

//Pad & Pad Terminal
/obj/machinery/piratepad
	name = "cargo hold pad"
	icon = 'icons/obj/telescience.dmi'
	icon_state = "lpad-idle-off"
	var/idle_state = "lpad-idle"
	var/warmup_state = "lpad-idle"
	var/sending_state = "lpad-beam"
	var/warmup_time = 10 SECONDS
	var/cargo_hold_id

/obj/machinery/piratepad/multitool_act(mob/living/user, obj/item/multitool/I)
	. = ..()
	if (istype(I))
		to_chat(user, "<span class='notice'>You register [src] in [I]s buffer.</span>")
		I.buffer = src
		return TRUE

/obj/machinery/piratepad/screwdriver_act(mob/living/user, obj/item/screwdriver/screw)
	. = ..()
	if(!.)
		return default_deconstruction_screwdriver(user, "lpad-idle-open", "lpad-idle-off", screw)

/obj/machinery/piratepad/crowbar_act(mob/living/user, obj/item/tool)
	. = ..()
	default_deconstruction_crowbar(tool)
	return TRUE

/obj/machinery/piratepad/RefreshParts()
	warmup_time = initial(warmup_time)

	var/parts_rating = 0
	var/i = 0
	for(var/obj/item/stock_parts/L in component_parts)
		parts_rating += L.rating
		++i
	// Average rating of all details
	var/rating = round_down(parts_rating / i)
	var/const/speed_up_per_rating = 26.6 // T4 = 80% speed up
	var/speed_up_ratio = max(ceil((rating-1) * speed_up_per_rating),0)/100
	warmup_time -= warmup_time*speed_up_ratio
	warmup_time = max(round(warmup_time, 0.1), 0)

/obj/machinery/computer/piratepad_control
	name = "cargo hold control terminal"
	//В этом терминале живёт весь прогресс антагонистов-грабителей. Разбитая консоль обнуляла
	//добычу за раунд и лишала команду цели, поэтому трюмный пульт неразрушаем. Гражданский
	//пульт наград ниже возвращает себе обычную хрупкость станционной машины.
	resistance_flags = INDESTRUCTIBLE | LAVA_PROOF | FIRE_PROOF | UNACIDABLE | ACID_PROOF
	var/status_report = "Ready for delivery."
	var/obj/machinery/piratepad/pad
	var/sending = FALSE
	var/points = 0
	///Всё, что через этот терминал вообще прошло. points - это остаток на счету, его обнуляет
	///снятие кредитов, и цель "собрать на N кредитов" оказывалась невыполненной у команды,
	///которая свою добычу уже обналичила.
	var/total_collected = 0
	var/datum/export_report/total_report
	var/sending_timer
	var/cargo_hold_id
	///Reference to the specific pad that the control computer is linked up to.
	var/datum/weakref/pad_ref

/obj/machinery/computer/piratepad_control/Initialize(mapload)
	..()
	return INITIALIZE_HINT_LATELOAD

/obj/machinery/computer/piratepad_control/Destroy()
	// Таймер прогрева send() держал бы терминал в SStimer, а loot-objective
	// пиратов/воксов/рейдеров - до конца раунда через cargo_hold
	deltimer(sending_timer)
	pad = null
	for(var/datum/objective/loot/booty in GLOB.objectives)
		if(booty.cargo_hold == src)
			booty.get_loot_value() //снимок набранного до того, как ссылка на терминал оборвётся
			booty.cargo_hold = null
	return ..()

/obj/machinery/computer/piratepad_control/multitool_act(mob/living/user, obj/item/multitool/I)
	. = ..()
	if (istype(I) && istype(I.buffer,/obj/machinery/piratepad))
		to_chat(user, "<span class='notice'>You link [src] with [I.buffer] in [I] buffer.</span>")
		pad_ref = WEAKREF(I.buffer)
		return TRUE

/obj/machinery/computer/piratepad_control/LateInitialize()
	. = ..()
	if(cargo_hold_id)
		for(var/obj/machinery/piratepad/P in GLOB.machines)
			if(P.cargo_hold_id == cargo_hold_id)
				pad_ref = WEAKREF(P)
				return
	else
		pad = locate() in range(4,src)
		pad_ref = WEAKREF(pad)

/obj/machinery/computer/piratepad_control/ui_interact(mob/user, datum/tgui/ui)
	ui = SStgui.try_update_ui(user, src, ui)
	if(!ui)
		ui = new(user, src, "CargoHoldTerminal", name)
		ui.open()

/obj/machinery/computer/piratepad_control/ui_data(mob/user)
	var/list/data = list()
	data["points"] = points
	data["pad"] = pad_ref?.resolve() ? TRUE : FALSE
	data["sending"] = sending
	data["status_report"] = status_report
	return data

/obj/machinery/computer/piratepad_control/ui_act(action, params)
	. = ..()
	if(.)
		return
	if(!pad_ref?.resolve())
		return

	switch(action)
		if("recalc")
			recalc()
			. = TRUE
		if("send")
			start_sending()
			. = TRUE
		if("stop")
			stop_sending()
			. = TRUE

/obj/machinery/computer/piratepad_control/AltClick(mob/user)
	. = ..()
	withdraw_points(user)

/obj/machinery/computer/piratepad_control/proc/withdraw_points(mob/living/user)
	if(!isliving(user))
		return
	if(!user.canUseTopic(src, BE_CLOSE))
		return
	if(machine_stat & (NOPOWER|BROKEN))
		return
	if(sending)
		to_chat(user, span_warning("[src] занят отправкой груза!"))
		return
	if(!points)
		to_chat(user, span_notice("На счету нет кредитов для снятия."))
		return
	to_chat(user, span_notice("Вы начинаете вывод средств с [src]..."))
	user.visible_message(span_notice("[user] подключается к [src] для снятия кредитов..."), span_notice("Вы подключаетесь к [src] для снятия кредитов..."))
	if(!do_after(user, 30 SECONDS, target = src))
		return
	if(QDELETED(src) || QDELETED(user))
		return
	if(!user.canUseTopic(src, BE_CLOSE) || (machine_stat & (NOPOWER|BROKEN)))
		return
	if(sending || !points)
		to_chat(user, span_warning("Снятие средств прервано."))
		return
	var/withdraw_amount = points
	points = 0
	new /obj/item/holochip(drop_location(), withdraw_amount)
	to_chat(user, span_notice("Вы сняли [withdraw_amount] кредитов с терминала."))
	playsound(src, 'sound/effects/cashregister.ogg', 50, TRUE)

/obj/machinery/computer/piratepad_control/proc/recalc()
	if(sending)
		return

	status_report = "Predicted value: "
	var/value = 0
	var/datum/export_report/ex = new
	var/obj/machinery/piratepad/pad = pad_ref?.resolve()
	for(var/atom/movable/AM in get_turf(pad))
		if(AM == pad)
			continue
		export_item_and_contents(AM, EXPORT_PIRATE | EXPORT_CARGO | EXPORT_CONTRABAND | EXPORT_EMAG, apply_elastic = FALSE, dry_run = TRUE, external_report = ex)

	for(var/datum/export/E in ex.total_amount)
		status_report += E.total_printout(ex,notes = FALSE)
		status_report += " "
		value += ex.total_value[E]

	if(!value)
		status_report += "0"

/obj/machinery/computer/piratepad_control/proc/send()
	if(!sending)
		return

	var/datum/export_report/ex = new
	var/obj/machinery/piratepad/pad = pad_ref?.resolve()

	var/queued_pirate_ransom = 0
	var/static/datum/export/pirate/ransom/pirate_ransom_datum
	if(!pirate_ransom_datum)
		pirate_ransom_datum = new
	for(var/atom/movable/AM in get_turf(pad))
		if(AM == pad)
			continue
		if(ishuman(AM))
			var/mob/living/carbon/human/held = AM
			var/earn = pirate_ransom_datum.get_cost(held)
			if(earn)
				// Same pipeline as /datum/syndicate_contract (extraction pod, station ransom, return) — not cargo qdel.
				var/datum/ransom_extraction/sequence = new
				sequence.start_for_pirate(held, get_turf(pad), 100 * rand(18, 45), earn, src)
				queued_pirate_ransom += earn
				continue
		export_item_and_contents(AM, EXPORT_PIRATE | EXPORT_CARGO | EXPORT_CONTRABAND | EXPORT_EMAG, apply_elastic = FALSE, delete_unsold = FALSE, external_report = ex)

	status_report = "Sold: "
	var/value = 0
	for(var/datum/export/E in ex.total_amount)
		var/export_text = E.total_printout(ex,notes = FALSE) //Don't want nanotrasen messages, makes no sense here.
		if(!export_text)
			continue

		status_report += export_text
		status_report += " "
		value += ex.total_value[E]

	if(queued_pirate_ransom)
		value += queued_pirate_ransom
		status_report += " +[queued_pirate_ransom] credits: hostage (extraction) "

	if(!total_report)
		total_report = ex
	else
		total_report.exported_atoms += ex.exported_atoms
		for(var/datum/export/E in ex.total_amount)
			total_report.total_amount[E] += ex.total_amount[E]
			total_report.total_value[E] += ex.total_value[E]
		// playsound(loc, 'sound/machines/wewewew.ogg', 70, TRUE)

	/// Ransom cr for pirates is applied in /datum/ransom_extraction/aftermath_capture; only ex items here.
	points += value
	total_collected += value
	if(queued_pirate_ransom)
		points -= queued_pirate_ransom
		total_collected -= queued_pirate_ransom

	if(!value)
		status_report += "Nothing"

	pad.visible_message("<span class='notice'>[pad] activates!</span>")
	flick(pad.sending_state,pad)
	pad.icon_state = pad.idle_state
	sending = FALSE

/obj/machinery/computer/piratepad_control/proc/start_sending()
	var/obj/machinery/piratepad/pad = pad_ref?.resolve()
	if(!pad)
		status_report = "No pad detected. Build or link a pad."
		audible_message(span_notice("[src] beeps."))
		return
	if(pad?.panel_open)
		status_report = "Please screwdrive pad closed to send. "
		pad.audible_message(span_notice("[pad] beeps."))
		return
	if(sending)
		return
	sending = TRUE
	status_report = "Sending... "
	pad.visible_message("<span class='notice'>[pad] starts charging up.</span>")
	pad.icon_state = pad.warmup_state
	if(pad.warmup_time)
		sending_timer = addtimer(CALLBACK(src,PROC_REF(send)),pad.warmup_time, TIMER_STOPPABLE)
	else
		send()

/obj/machinery/computer/piratepad_control/proc/stop_sending(custom_report)
	if(!sending)
		return
	sending = FALSE
	status_report = "Ready for delivery."
	if(custom_report)
		status_report = custom_report
	pad.icon_state = pad.idle_state
	deltimer(sending_timer)

/datum/export/pirate
	export_category = EXPORT_PIRATE

//Attempts to find the thing on station
/datum/export/pirate/proc/find_loot()
	return

/datum/export/pirate/ransom
	cost = 60000
	unit_name = "hostage"
	export_types = list(/mob/living/carbon/human)

/datum/export/pirate/ransom/find_loot()
	var/list/head_minds = SSjob.get_living_heads()
	var/list/head_mobs = list()
	for(var/datum/mind/M in head_minds)
		head_mobs += M.current
	if(head_mobs.len)
		return pick(head_mobs)

/datum/export/pirate/ransom/get_cost(atom/movable/AM)
	var/mob/living/carbon/human/H = AM
	if(H.stat != CONSCIOUS || !H.mind || !H.mind.assigned_role) //mint condition only
		return FALSE
	else if("pirate" in H.faction) //can't ransom your fellow pirates to CentCom!
		return FALSE
	else
		if(H.mind.assigned_role in GLOB.command_positions)
			return 100000
		else
			return 50000

/datum/export/pirate/parrot
	cost = 50000
	unit_name = "alive parrot"
	export_types = list(/mob/living/simple_animal/parrot)

/datum/export/pirate/parrot/find_loot()
	for(var/mob/living/simple_animal/parrot/P in GLOB.alive_mob_list)
		var/turf/T = get_turf(P)
		if(T && is_station_level(T.z))
			return P

/datum/export/pirate/cash
	cost = 1
	unit_name = "bills"
	export_types = list(/obj/item/stack/spacecash)

/datum/export/pirate/cash/get_amount(obj/O)
	var/obj/item/stack/spacecash/C = O
	return ..() * C.amount * C.value

/datum/export/pirate/holochip
	cost = 1
	unit_name = "holochip"
	export_types = list(/obj/item/holochip)

/datum/export/pirate/holochip/get_cost(atom/movable/AM)
	var/obj/item/holochip/H = AM
	return H.credits

/obj/item/clothing/head/helmet/space/pirate/eva
	name = "Modified EVA helmet"
	desc = "A modified helmet to allow space pirates to intimidate their customers whilst staying safe from the void. Comes with some additional protection."
	icon_state = "spacepirate"
	item_state = "space_pirate_helmet"
	armor = list(MELEE = 20, BULLET = 40, LASER = 30, ENERGY = 25, BOMB = 50, BIO = 100, RAD = 50, FIRE = 80, ACID = 80, WOUND = 20)
	strip_delay = 40
	equip_delay_other = 20
	//species_restricted = list("Vox")

/obj/item/clothing/head/helmet/space/pirate/bandana/eva
	icon_state = "spacebandana"
	item_state = "space_bandana_helmet"

/obj/item/clothing/suit/space/pirate/eva
	name = "Modified EVA suit"
	desc = "A modified suit to allow space pirates to board shuttles and stations while avoiding the maw of the void. Comes with additional protection and is lighter to move in."
	icon_state = "spacepirate"
	w_class = WEIGHT_CLASS_NORMAL
	allowed = list(/obj/item/gun, /obj/item/ammo_box, /obj/item/ammo_casing, /obj/item/melee/baton, /obj/item/restraints/handcuffs, /obj/item/tank/internals, /obj/item/melee/transforming/energy/sword/pirate, /obj/item/clothing/glasses/cover/eyepatch, /obj/item/reagent_containers/food/drinks/bottle/rum)
	slowdown = 0
	armor = list(MELEE = 20, BULLET = 40, LASER = 30,ENERGY = 25, BOMB = 50, BIO = 100, RAD = 50, FIRE = 80, ACID = 80, WOUND = 20)
	strip_delay = 40
	equip_delay_other = 20
	//species_restricted = list("Vox")
