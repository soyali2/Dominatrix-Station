/datum/round_event_control/alien_infestation
	name = "Alien Infestation"
	typepath = /datum/round_event/ghost_role/alien_infestation
	weight = 5
	// На типичных 30-40 тяжёлый трек гост-пула состоял из одного мага: порог 40 открывал
	// улей только на пиковом онлайне. 35 не дожал (медиум-экипаж в логах 9766-9775 = 22-33,
	// улей не выпал ни разу) - 30 открывает пиковый медиум и весь хард.
	min_players = 30
	max_occurrences = 1
	// Единственный 0-гейтный heavy: в харде копилка могла залочить улей до 20-й минуты.
	// 25 мин - позже лёгкой волны (20), раньше основной (30), как у ниндзя.
	earliest_start = 25 MINUTES
	category = EVENT_CATEGORY_ENTITIES
	severity = DIRECTOR_SEVERITY_GHOST // антаги из призраков - гост-пул, а не общий MAJOR
	cost = 15
	// Улей стартует с 1-2 личинок и разгоняется полчаса: 45 сразу съедала бы всю
	// антаг-цель медиума (30 попа * 1.5) и глушила пул при пустом вент-крабе.
	// Разросшийся улей докрутит множитель активности директора сам.
	intensity = 30
	director_ghost_jobban = ROLE_ALIEN
	director_ghost_preference = ROLE_ALIEN
	intensity_linger = 45 MINUTES // улей растёт заметно дольше спавнера
	antag_heavy = TRUE // угроза всей станции: мягкие профили такое выключают
	family = "xenomorph" // с рулсетом-двойником динамика: не подряд
	required_round_type = list(ROUNDTYPE_DYNAMIC_TEAMBASED, ROUNDTYPE_DYNAMIC_HARD, ROUNDTYPE_DYNAMIC_MEDIUM) // не экста и не лайт
	description = "A xenomorph larva spawns on a random vent."

/datum/round_event/ghost_role/alien_infestation
	announce_when	= 400

	minimum_required = 1
	role_name = "Личинка Ксеноморфа"

	// 50% chance of being incremented by one
	var/spawncount = 1
	var/successSpawn = 0	//So we don't make a command report if nothing gets spawned.
	fakeable = TRUE


/datum/round_event/ghost_role/alien_infestation/setup()
	announce_when = rand(announce_when, announce_when + 50)
	if(prob(50))
		spawncount++

/datum/round_event/ghost_role/alien_infestation/kill()
	if(!successSpawn && control)
		// This never happened, so let's not deny the future of this round
		// some xenolovin
		control.occurrences--
	return ..()

/datum/round_event/ghost_role/alien_infestation/announce(fake)
	if(successSpawn || fake)
		priority_announce("Вспышка биологической угрозы 4-го уровня зафиксирована на борту станции [station_name()]. Всему персоналу надлежит сдержать её распространение любой ценой!", "ВНИМАНИЕ: БИОЛОГИЧЕСКАЯ УГРОЗА", 'sound/effects/siren-spooky.ogg', has_important_message = TRUE)


/datum/round_event/ghost_role/alien_infestation/spawn_role()
	var/list/vents = list()
	for(var/obj/machinery/atmospherics/components/unary/vent_pump/temp_vent in GLOB.machines)
		if(QDELETED(temp_vent))
			continue
		if(is_station_level(temp_vent.loc.z) && !temp_vent.welded)
			var/datum/pipeline/temp_vent_parent = temp_vent.parents[1]
			//Stops Aliens getting stuck in small networks.
			//See: Security, Virology
			if(temp_vent_parent.other_atmosmch.len > 20)
				vents += temp_vent

	if(!vents.len)
		message_admins("An event attempted to spawn an alien but no suitable vents were found. Shutting down.")
		return MAP_ERROR

	var/list/candidates = get_candidates(ROLE_ALIEN, null, ROLE_ALIEN)

	if(!candidates.len)
		return NOT_ENOUGH_PLAYERS

	while(spawncount > 0 && vents.len && candidates.len)
		var/obj/vent = pick_n_take(vents)
		var/client/C = pick_n_take(candidates)

		var/mob/living/carbon/alien/larva/new_xeno = new(vent.loc)
		new_xeno.key = C.key

		spawncount--
		successSpawn = TRUE
		message_admins("[ADMIN_LOOKUPFLW(new_xeno)] has been made into an alien by an event.")
		log_game("[key_name(new_xeno)] was spawned as an alien by an event.")
		spawned_mobs += new_xeno

	if(successSpawn)
		return SUCCESSFUL_SPAWN
	else
		// Like how did we get here?
		return FALSE
