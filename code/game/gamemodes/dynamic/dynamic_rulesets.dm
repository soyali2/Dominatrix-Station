#define REVOLUTION_VICTORY 1
#define STATION_VICTORY 2

/datum/dynamic_ruleset
	parent_type = /datum/director_action
	director_kind = DIRECTOR_KIND_RULESET
	severity = DIRECTOR_SEVERITY_ANTAG
	/// For admin logging and round end screen.
	// If you want to change this variable name, the force latejoin/midround rulesets
	// to not use sortNames.
	var/name = ""
	/// For admin logging and round end screen, do not change this unless making a new rule type.
	var/ruletype = ""
	/// If set to TRUE, the rule won't be discarded after being executed, and dynamic will call rule_process() every time it ticks.
	var/persistent = FALSE
	/// If set to TRUE, dynamic mode will be able to draft this ruleset again later on. (doesn't apply for roundstart rules)
	var/repeatable = FALSE
	/// If set higher than 0 decreases weight by itself causing the ruleset to appear less often the more it is repeated.
	var/repeatable_weight_decrease = 2
	/// List of players that are being drafted for this rule
	var/list/mob/candidates = list()
	/// Исполнение уже запланировано (execute_action -> addtimer(execute_scheduled_ruleset)):
	/// снапшоты кандидатов нужны отложенному execute(), отпускать их сейчас нельзя.
	var/execution_pending = FALSE
	/// List of players that were selected for this rule
	var/list/datum/mind/assigned = list()
	/// Preferences flag such as ROLE_WIZARD that need to be turned on for players to be antag
	var/antag_flag = null
	/// Treat this ruleset's antagonist preference as enabled for every otherwise eligible player.
	var/force_antag_preference = FALSE
	/// The antagonist datum that is assigned to the mobs mind on ruleset execution.
	var/datum/antagonist/antag_datum = null
	/// The required minimum account age for this ruleset.
	var/minimum_required_age = 0 // BLUEMOON EDIT - было 7
	/// If set, and config flag protect_roles_from_antagonist is false, then the rule will not pick players from these roles.
	var/list/protected_roles = list()
	/// If set, rule will deny candidates from those roles always.
	var/list/restricted_roles = list()
	/// If set, rule will only accept candidates from those roles. If on a roundstart ruleset, requires the player to have the correct antag pref enabled and any of the possible roles enabled.
	var/list/exclusive_roles = list()
	/// If set, there needs to be a certain amount of players doing those roles (among the players who won't be drafted) for the rule to be drafted IMPORTANT: DOES NOT WORK ON ROUNDSTART RULESETS.
	var/list/enemy_roles = list()
	/// If enemy_roles was set, this is the amount of enemy job workers needed per threat_level range (0-10,10-20,etc) IMPORTANT: DOES NOT WORK ON ROUNDSTART RULESETS.
	var/required_enemies = list(1,1,0,0,0,0,0,0,0,0)
	/// The rule needs this many candidates (post-trimming) to be executed (example: Cult needs 4 players at round start)
	var/required_candidates = 0
	/// Cost per level the rule scales up.
	var/scaling_cost = 0
	/// How many times a rule has scaled up upon getting picked.
	var/scaled_times = 0
	/// Подтверждённая фактически потраченная стоимость запусков. Помимо roundend-отчёта служит
	/// бухгалтерией страховки рано потерянных антаг-ролей директора.
	var/total_cost = 0
	/// Стоимость уже списана, но асинхронный execute ещё не подтвердил выдачу ролей.
	var/director_pending_cost = 0
	/// mind -> list(amount, at, activity): индивидуальная страховая доля подтверждённой роли.
	var/list/director_loss_refund_values = list()
	/// mind -> TRUE: роль уже была застрахована и не может получить покрытие повторно.
	var/list/director_loss_accounted = list()
	/// A flag that determines how the ruleset is handled. Check __DEFINES/dynamic.dm for an explanation of the accepted values.
	var/flags = NONE
	/// Pop range per requirement. If zero defaults to mode's pop_per_requirement.
	var/pop_per_requirement = 5 //BLUEMOON CHANGES
	/// Requirements are the threat level requirements per pop range.
	/// With the default values, The rule will never get drafted below 10 threat level (aka: "peaceful extended"), and it requires a higher threat level at lower pops.
	var/list/requirements = list(40,30,20,10,10,10,10,10,10,10)
	/// Reference to the mode, use this instead of SSticker.mode.
	var/datum/game_mode/dynamic/mode = null
	/// If a role is to be considered another for the purpose of banning.
	var/antag_flag_override = null
	/// If a ruleset type which is in this list has been executed, then the ruleset will not be executed.
	var/list/blocking_rules = list()
	/// The maximum amount of players required for the rule to be considered.
	/// Anything below zero or exactly zero is ignored.
	var/maximum_players = 0
	/// Calculated during acceptable(), used in scaling and team sizes.
	var/indice_pop = 0
	/// Base probability used in scaling. The higher it is, the more likely to scale. Kept as a var to allow for config editing._SendSignal(sigtype, list/arguments)
	var/base_prob = 60
	/// Delay for when execute will get called from the time of post_setup (roundstart) or process (midround/latejoin).
	/// Make sure your ruleset works with execute being called during the game when using this, and that the clean_up proc reverts it properly in case of faliure.
	var/delay = 0
	/// Человекочитаемая причина последнего провала execute(); попадает в историю директора.
	var/execution_failure_reason = null
	/// world.time запуска рулсета директором (штамп в SSdirector.note_fired): возраст исполнения
	/// для затухания вклада в intensity. 0 у раундстартов - их возраст считается от старта раунда.
	var/executed_at = 0

	/// Judges the amount of antagonists to apply, for both solo and teams.
	/// Note that some antagonists (such as traitors, lings, heretics, etc) will add more based on how many times they've been scaled.
	/// Written as a linear equation--ceil(x/denominator) + offset, or as a fixed constant.
	/// If written as a linear equation, will be in the form of `list("denominator" = denominator, "offset" = offset).
	var/antag_cap = 0

	/// Если GLOB.round_type (выставляется через голосование или админами) нет в списке, то рулсет не может выпасть.
	/// Переопределяет унаследованный дефолт null ("любые типы раунда"), чтобы не менять поведение после переезда на director_action.
	required_round_type = list(ROUNDTYPE_DYNAMIC_TEAMBASED, ROUNDTYPE_DYNAMIC_HARD, ROUNDTYPE_DYNAMIC_MEDIUM, ROUNDTYPE_DYNAMIC_LIGHT)

/datum/dynamic_ruleset/New()
	// Rulesets can be instantiated more than once, such as when an admin clicks
	// "Execute Midround Ruleset". Thus, it would be wrong to perform any
	// side effects here. Dynamic rulesets should be stateless anyway.
	SHOULD_NOT_OVERRIDE(TRUE)

	mode = SSticker.mode

	..()

/datum/dynamic_ruleset/action_name()
	return name

/datum/dynamic_ruleset/roundstart // One or more of those drafted at roundstart
	ruletype = "Roundstart"

// Can be drafted when a player joins the server
/datum/dynamic_ruleset/latejoin
	ruletype = "Latejoin"

/// Кандидат уже установлен, trim/ready вызваны в SSdirector.on_latejoin; бюджет списан директором.
/// Остаётся отложенно исполнить рулсет с учётом его delay.
/datum/dynamic_ruleset/latejoin/execute_action()
	execution_pending = TRUE
	addtimer(CALLBACK(mode, TYPE_PROC_REF(/datum/game_mode/dynamic, execute_scheduled_ruleset), src), delay)
	return TRUE

/// By default, a rule is acceptable if it satisfies the threat level/population requirements.
/// If your rule has extra checks, such as counting security officers, do that in ready() instead
/datum/dynamic_ruleset/proc/acceptable(population = 0, threat_level = 0)
	pop_per_requirement = pop_per_requirement > 0 ? pop_per_requirement : mode.pop_per_requirement
	indice_pop = min(requirements.len,round(population/pop_per_requirement)+1)

	if(min_players > population)
		SSblackbox.record_feedback("tally","dynamic",1,"Times rulesets rejected due to low pop")
		return FALSE
	if(maximum_players > 0 && population > maximum_players)
		SSblackbox.record_feedback("tally","dynamic",1,"Times rulesets rejected due to high pop")
		return FALSE
	return (threat_level >= requirements[indice_pop])

/// Проверки директора поверх acceptable() (порог по популяции/трету раунда).
/// Здесь же гейты only_ruleset, повторяемости, blocking_rules и стекинга HIGH_IMPACT.
/// mode может быть null вне раунда (юнит-тесты) - тогда остальные проверки неприменимы.
/datum/dynamic_ruleset/can_fire(datum/director_signals/signals)
	. = ..()
	if(!.)
		return
	if(!acceptable(signals.effective_crew, mode ? mode.threat_level : 0))
		return FALSE
	if(!mode)
		return TRUE
	if(mode.only_ruleset_executed)
		return FALSE
	// Неповторяемый рулсет, уже отработавший, больше не выбирается.
	if(!repeatable && (src in mode.executed_rules))
		return FALSE
	if(mode.check_blocking(blocking_rules, mode.executed_rules))
		return FALSE
	// Стекинг раунд-эндеров: без стекинга второй HIGH_IMPACT не выпадет, пока трет ниже лимита.
	if((flags & HIGH_IMPACT_RULESET) && mode.high_impact_ruleset_executed \
		&& mode.threat_level < GLOB.dynamic_stacking_limit && GLOB.dynamic_no_stacking)
		return FALSE
	return TRUE

/// When picking rulesets, if dynamic picks the same one multiple times, it will "scale up".
/// However, doing this blindly would result in lowpop rounds (think under 10 people) where over 80% of the crew is antags!
/// This function is here to ensure the antag ratio is kept under control while scaling up.
/// Returns how much threat to actually spend in the end.
/datum/dynamic_ruleset/proc/scale_up(population, max_scale)
	if (!scaling_cost)
		return FALSE

	var/antag_fraction = 0
	for(var/_ruleset in (mode.executed_rules + list(src))) // we care about the antags we *will* assign, too
		var/datum/dynamic_ruleset/ruleset = _ruleset
		antag_fraction += ((1 + ruleset.scaled_times) * ruleset.get_antag_cap(population)) / mode.roundstart_pop_ready

	for(var/i in 1 to max_scale)
		if(antag_fraction < 0.25)
			scaled_times += 1
			antag_fraction += get_antag_cap(population) / mode.roundstart_pop_ready // we added new antags, gotta update the %

	return scaled_times * scaling_cost

/// Returns what the antag cap with the given population is.
/datum/dynamic_ruleset/proc/get_antag_cap(population)
	if (isnum(antag_cap))
		return antag_cap

	return CEILING(population / antag_cap["denominator"], 1) + (antag_cap["offset"] || 0)

/// This is called if persistent variable is true everytime SSTicker ticks.
/datum/dynamic_ruleset/proc/rule_process()
	return

/// Called on game mode pre_setup for roundstart rulesets.
/// Do everything you need to do before job is assigned here.
/// IMPORTANT: ASSIGN special_role HERE
/datum/dynamic_ruleset/proc/pre_execute()
	return TRUE

/// Called on post_setup on roundstart and when the rule executes on midround and latejoin.
/// Give your candidates or assignees equipment and antag datum here.
/datum/dynamic_ruleset/proc/execute()
	for(var/datum/mind/M in assigned)
		M.add_antag_datum(antag_datum)
	return TRUE

/// Текст подтверждения для истории директора. assigned_this_attempt, а не длина assigned:
/// повторяемые рулсеты хранят предыдущие назначения для живого учёта intensity.
/datum/dynamic_ruleset/proc/director_execution_detail(assigned_this_attempt)
	return "исполнение подтверждено; назначено ролей: [assigned_this_attempt]"

/// Here you can perform any additional checks you want. (such as checking the map etc)
/// Remember that on roundstart no one knows what their job is at this point.
/// IMPORTANT: If ready() returns TRUE, that means pre_execute() or execute() should never fail!
/datum/dynamic_ruleset/proc/ready(forced = 0)
	if (required_candidates > candidates.len)
		SSblackbox.record_feedback("tally","dynamic",1,"Times rulesets rejected due to not enough candidates")
		return FALSE
	return TRUE

/// Runs from gamemode process() if ruleset fails to start, like delayed rulesets not getting valid candidates.
/// This one only handles refunding the threat, override in ruleset to clean up the rest.
/datum/dynamic_ruleset/proc/clean_up()
	mode.refund_threat(src, cost + (scaled_times * scaling_cost))
	mode.threat_log += "[worldtime2text()]: [ruletype] [name] refunded [cost + (scaled_times * scaling_cost)]. Failed to execute."

/// Отпустить снапшоты кандидатов. Датумы рулсетов живут до конца раунда, а trim_candidates()
/// зовётся из preflight каждые несколько секунд - без отпускания последняя пачка ссылок
/// на мобов висит на датуме вечно (прод-harddel обсервера в list_observers у nuclear).
/// Звать после того, как потребитель снапшота (preflight/execute) закончил.
/datum/dynamic_ruleset/proc/release_candidate_snapshots()
	candidates.Cut()

/// Gets weight of the ruleset
/// Note that this decreases weight if repeatable is TRUE and repeatable_weight_decrease is higher than 0
/// Note: If you don't want repeatable rulesets to decrease their weight use the weight variable directly
/// Сигнатура с опциональным signals переопределяет базовый director_action/get_weight(); существующие вызовы rule.get_weight() без аргументов не ломаются.
/datum/dynamic_ruleset/get_weight(datum/director_signals/signals)
	var/effective_weight = weight
	// Оценка пула и перерисовка панели не являются новыми запусками: базовый вес
	// сохраняется, а штраф считается только по уже исполненным рулсетам.
	if(mode && repeatable && effective_weight > 1 && repeatable_weight_decrease > 0)
		for(var/datum/dynamic_ruleset/DR in mode.executed_rules)
			if(istype(DR, type))
				effective_weight = max(effective_weight-repeatable_weight_decrease,1)
	return effective_weight

/// Here you can remove candidates that do not meet your requirements.
/// This means if their job is not correct or they have disconnected you can remove them from candidates here.
/// Usually this does not need to be changed unless you need some specific requirements from your candidates.
/datum/dynamic_ruleset/proc/trim_candidates()
	return

/// Whether a candidate opted into this antagonist role, including rulesets that explicitly force the preference on.
/datum/dynamic_ruleset/proc/has_required_antag_preference(client/candidate_client)
	if(force_antag_preference)
		return TRUE
	if(!candidate_client)
		return FALSE
	var/role_preference = antag_flag_override ? antag_flag_override : antag_flag
	return HAS_ANTAG_PREF(candidate_client, role_preference)

/// Set mode result and news report here.
/// Only called if ruleset is flagged as HIGH_IMPACT_RULESET
/datum/dynamic_ruleset/proc/round_result()

//////////////////////////////////////////////
//                                          //
//           ROUNDSTART RULESETS            //
//                                          //
//////////////////////////////////////////////

/// Checks if candidates are connected and if they are banned or don't want to be the antagonist.
/datum/dynamic_ruleset/roundstart/trim_candidates()
	for(var/mob/dead/new_player/candidate_player in candidates)
		var/client/candidate_client = GET_CLIENT(candidate_player)
		if (!candidate_client || !candidate_player.mind) // Are they connected?
			candidates.Remove(candidate_player)
			continue

		else if(!mode.check_age(candidate_client, minimum_required_age))
			candidates.Remove(candidate_player)
			continue

		if(candidate_player.mind.special_role) // We really don't want to give antag to an antag.
			candidates.Remove(candidate_player)
			continue

		if(!has_required_antag_preference(candidate_client))
			candidates.Remove(candidate_player)
			continue

		var/role_to_bancheck = antag_flag_override ? antag_flag_override : antag_flag
		if(role_to_bancheck && (jobban_isbanned(candidate_player, role_to_bancheck) || QDELETED(candidate_player)))
			candidates.Remove(candidate_player)
			continue
		if(jobban_isbanned(candidate_player, ROLE_INTEQ) || QDELETED(candidate_player))
			candidates.Remove(candidate_player)
			continue

		// If this ruleset has exclusive_roles set, we want to only consider players who have those
		// job prefs enabled and are eligible to play that job. Otherwise, continue as before.
		if(length(exclusive_roles))
			var/exclusive_candidate = FALSE
			for(var/role in exclusive_roles)
				var/datum/job/job = SSjob.GetJob(role)
				if((role in candidate_client.prefs.job_preferences) && !jobban_isbanned(candidate_player.ckey, role) && !job.required_playtime_remaining(candidate_client) /*BLUEMOON*/&& !job.is_species_blacklisted(candidate_client)/*BLUEMOON*/)
					exclusive_candidate = TRUE
					break

			// If they didn't have any of the required job prefs enabled or were banned from all enabled prefs,
			// they're not eligible for this antag type.
			if(!exclusive_candidate)
				candidates.Remove(candidate_player)

/// Do your checks if the ruleset is ready to be executed here.
/// Should ignore certain checks if forced is TRUE
/datum/dynamic_ruleset/roundstart/ready(population, forced = FALSE)
	return ..()
