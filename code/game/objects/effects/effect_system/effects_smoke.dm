/////////////////////////////////////////////
//// SMOKE SYSTEMS
/////////////////////////////////////////////

/obj/effect/particle_effect/smoke
	name = "smoke"
	icon = 'icons/effects/96x96.dmi'
	icon_state = "smoke"
	pixel_x = -32
	pixel_y = -32
	opacity = 0
	layer = FLY_LAYER
	anchored = TRUE
	mouse_opacity = MOUSE_OPACITY_TRANSPARENT
	animate_movement = 0
	var/amount = 4
	var/lifetime = 5
	var/opaque = 1 //whether the smoke can block the view when in enough amount


/obj/effect/particle_effect/smoke/proc/fade_out(frames = 16)
	if(alpha == 0) //Handle already transparent case
		return
	if(frames == 0)
		frames = 1 //We will just assume that by 0 frames, the coder meant "during one frame".
	var/step = alpha / frames
	for(var/i = 0, i < frames, i++)
		alpha -= step
		if(alpha < 160)
			set_opacity(0) //if we were blocking view, we aren't now because we're fading out
		stoplag()

/obj/effect/particle_effect/smoke/Initialize(mapload)
	. = ..()
	create_reagents(500, NONE, NO_REAGENTS_VALUE)
	START_PROCESSING(SSobj, src)


/obj/effect/particle_effect/smoke/Destroy()
	STOP_PROCESSING(SSobj, src)
	return ..()

/obj/effect/particle_effect/smoke/proc/kill_smoke()
	STOP_PROCESSING(SSobj, src)
	INVOKE_ASYNC(src, PROC_REF(fade_out))
	QDEL_IN(src, 10)

/obj/effect/particle_effect/smoke/process()
	lifetime--
	if(lifetime < 1)
		kill_smoke()
		return FALSE
	for(var/mob/living/L in range(0,src))
		smoke_mob(L)
	return TRUE

/obj/effect/particle_effect/smoke/proc/smoke_mob(mob/living/carbon/C)
	if(!istype(C))
		return FALSE
	if(lifetime<1)
		return FALSE
	if(C.internal != null || C.has_smoke_protection())
		return FALSE
	if(C.smoke_delay)
		return FALSE
	C.smoke_delay++
	addtimer(CALLBACK(src, PROC_REF(remove_smoke_delay), C), 10)
	return TRUE

/obj/effect/particle_effect/smoke/proc/remove_smoke_delay(mob/living/carbon/C)
	if(C)
		C.smoke_delay = 0

/obj/effect/particle_effect/smoke/proc/spread_smoke()
	var/turf/t_loc = get_turf(src)
	if(!t_loc)
		return
	var/list/newsmokes = list()
	for(var/turf/T in t_loc.GetAtmosAdjacentTurfs())
		var/obj/effect/particle_effect/smoke/foundsmoke = locate() in T //Don't spread smoke where there's already smoke!
		if(foundsmoke)
			continue
		for(var/mob/living/L in T)
			smoke_mob(L)
		var/obj/effect/particle_effect/smoke/S = new type(T)
		reagents?.copy_to(S, reagents?.total_volume)
		S.setDir(pick(GLOB.cardinals))
		S.amount = amount-1
		S.add_atom_colour(color, FIXED_COLOUR_PRIORITY)
		S.lifetime = lifetime
		if(S.amount>0)
			if(opaque)
				S.set_opacity(TRUE)
			newsmokes.Add(S)

	if(newsmokes.len)
		spawn(1) //the smoke spreads rapidly but not instantly
			for(var/obj/effect/particle_effect/smoke/SM in newsmokes)
				SM.spread_smoke()


/datum/effect_system/smoke_spread
	var/amount = 10
	effect_type = /obj/effect/particle_effect/smoke

/datum/effect_system/smoke_spread/set_up(radius = 5, loca)
	if(isturf(loca))
		location = loca
	else
		location = get_turf(loca)
	amount = radius

/datum/effect_system/smoke_spread/start()
	if(holder)
		location = get_turf(holder)
	var/obj/effect/particle_effect/smoke/S = new effect_type(location)
	S.amount = amount
	if(S.amount)
		S.spread_smoke()


/////////////////////////////////////////////
// Bad smoke
/////////////////////////////////////////////

/obj/effect/particle_effect/smoke/bad
	lifetime = 8

/obj/effect/particle_effect/smoke/bad/smoke_mob(mob/living/carbon/C)
	. = ..()
	if(!.)
		return
	C.drop_all_held_items()
	C.adjustOxyLoss(1)
	C.emote("cough")

/obj/effect/particle_effect/smoke/bad/Crossed(atom/movable/AM, oldloc)
	. = ..()
	if(istype(AM, /obj/item/projectile/beam))
		var/obj/item/projectile/beam/B = AM
		B.damage = (B.damage/2)

/datum/effect_system/smoke_spread/bad
	effect_type = /obj/effect/particle_effect/smoke/bad

/////////////////////////////////////////////
// Nanofrost smoke
/////////////////////////////////////////////

/obj/effect/particle_effect/smoke/freezing
	name = "nanofrost smoke"
	color = "#B2FFFF"
	opaque = 0

/datum/effect_system/smoke_spread/freezing
	effect_type = /obj/effect/particle_effect/smoke/freezing
	var/blast = 0
	var/temperature = 2
	var/weldvents = TRUE
	var/distcheck = TRUE

/datum/effect_system/smoke_spread/freezing/proc/Chilled(atom/A)
	if(isopenturf(A))
		var/turf/open/T = A
		if(T.air)
			var/datum/gas_mixture/G = T.air
			if(!distcheck || get_dist(T, location) < blast) // Otherwise we'll get silliness like people using Nanofrost to kill people through walls with cold air
				G.set_temperature(temperature)
			T.air_update_turf()
			for(var/obj/effect/hotspot/H in T)
				qdel(H)
			if(G.get_moles(GAS_PLASMA))
				G.adjust_moles(GAS_N2, G.get_moles(GAS_PLASMA))
				G.set_moles(GAS_PLASMA, 0)
		if (weldvents)
			for(var/obj/machinery/atmospherics/components/unary/U in T)
				if(!isnull(U.welded) && !U.welded) //must be an unwelded vent pump or vent scrubber.
					U.welded = TRUE
					U.update_icon()
					U.visible_message("<span class='danger'>[U] was frozen shut!</span>")
		for(var/mob/living/L in T)
			L.ExtinguishMob()
		for(var/obj/item/Item in T)
			Item.extinguish()

/datum/effect_system/smoke_spread/freezing/set_up(radius = 5, loca, blast_radius = 0)
	..()
	blast = blast_radius

/datum/effect_system/smoke_spread/freezing/start()
	if(blast)
		for(var/turf/T in RANGE_TURFS(blast, location))
			Chilled(T)
	..()

/datum/effect_system/smoke_spread/freezing/decon
	temperature = 293.15
	distcheck = FALSE
	weldvents = FALSE


/////////////////////////////////////////////
// Sleep smoke
/////////////////////////////////////////////

/obj/effect/particle_effect/smoke/sleeping
	color = "#9C3636"
	lifetime = 10

/obj/effect/particle_effect/smoke/sleeping/smoke_mob(mob/living/carbon/C)
	. = ..()
	if(!.)
		return
	C.Sleeping(200)
	C.emote("cough")

/datum/effect_system/smoke_spread/sleeping
	effect_type = /obj/effect/particle_effect/smoke/sleeping

/////////////////////////////////////////////
// Chem smoke
/////////////////////////////////////////////

/obj/effect/particle_effect/smoke/chem
	lifetime = 10


/obj/effect/particle_effect/smoke/chem/process()
	. = ..()
	if(!.)
		return
	var/turf/T = get_turf(src)
	var/fraction = 1/initial(lifetime)
	for(var/atom/movable/AM in T)
		if(AM.type == src.type)
			continue
		if((T.turf_flags & TURF_INTACT) && AM.level == 1) //hidden under the floor
			continue
		reagents.reaction(AM, TOUCH, fraction)

	reagents.reaction(T, TOUCH, fraction)

/obj/effect/particle_effect/smoke/chem/smoke_mob(mob/living/carbon/C)
	. = ..()
	if(!.)
		return
	var/fraction = 1/initial(lifetime)
	reagents.copy_to(C, fraction*reagents.total_volume)
	reagents.reaction(C, INGEST, fraction)

/datum/effect_system/smoke_spread/chem
	/// Носитель химии до момента рождения дыма. Голый /obj, у которого reagents.my_atom
	/// смотрит обратно на него же - ссылочный цикл, а рефкаунт BYOND циклы не разбирает
	/// никогда. Разорвать его может только Destroy(), то есть qdel самой системы.
	var/obj/chemholder
	effect_type = /obj/effect/particle_effect/smoke/chem
	// Система одноразовая и убирает себя сама в конце start().
	//
	// Ни одно из ~48 мест создания химдыма qdel не звало: систему заводили локальной
	// переменной, дёргали set_up()/start() и бросали. Из-за цикла выше каждый такой пуск
	// оставлял в мире НАВСЕГДА голый /obj + /datum/reagents(500) + датумы реагентов.
	// В переписи прода (раунды 10050/10052/10054, 100-130 игроков) счётчик голых /obj рос
	// на +122..+644 за каждый 25-минутный интервал и не убыл ни разу.
	//
	// Почему уборка, а не замена /obj на чистый /datum/reagents (путь tg): сама по себе
	// она течь не лечит - reagent_list -> reagent.holder -> reagents тоже цикл, то есть
	// брошенная система утекала бы ровно так же, только на один объект меньше. Лечит
	// именно управление временем жизни, а не тип носителя.
	//
	// Цена решения: экземпляр нельзя переиспользовать, повторный start() будет холостым.
	// Сверено по всему дереву - ни одного chem/foam-экземпляра в переменной объекта нет,
	// все места создания локальные; в переменных живут только spark_spread, trail_follow
	// и бесхимийные smoke_spread (мех, дымовая граната, танцпол дьявола), у которых
	// chemholder'а нет вовсе. Кому переиспользование понадобится - выставить
	// autocleanup = FALSE и звать qdel() самому.
	autocleanup = TRUE

/datum/effect_system/smoke_spread/chem/New()
	..()
	chemholder = new /obj()
	var/datum/reagents/R = new/datum/reagents(500)
	chemholder.reagents = R
	R.my_atom = chemholder

/datum/effect_system/smoke_spread/chem/Destroy()
	qdel(chemholder)
	chemholder = null
	return ..()

/datum/effect_system/smoke_spread/chem/set_up(datum/reagents/carry = null, radius = 1, loca, silent = FALSE)
	if(isturf(loca))
		location = loca
	else
		location = get_turf(loca)
	amount = radius
	carry.copy_to(chemholder, carry.total_volume)

	if(!silent)
		var/contained = ""
		for(var/reagent in carry.reagent_list)
			contained += " [reagent] "
		if(contained)
			contained = "\[[contained]\]"

		var/where = "[AREACOORD(location)]"
		if(carry.my_atom && carry.my_atom.fingerprintslast)
			var/mob/M = get_mob_by_key(carry.my_atom.fingerprintslast)
			var/more = ""
			if(M)
				more = "[ADMIN_LOOKUPFLW(M)] "
			message_admins("Smoke: ([ADMIN_VERBOSEJMP(location)])[contained]. Key: [more ? more : carry.my_atom.fingerprintslast].")
			log_game("A chemical smoke reaction has taken place in ([where])[contained]. Last touched by [carry.my_atom.fingerprintslast].")
		else
			message_admins("Smoke: ([ADMIN_VERBOSEJMP(location)])[contained]. No associated key.")
			log_game("A chemical smoke reaction has taken place in ([where])[contained]. No associated key.")


/datum/effect_system/smoke_spread/chem/start()
	// Система себя уже убрала (см. autocleanup у типа) - chemholder отпущен, дальше идти
	// некуда. Гард такой же, как у /datum/effect_system/start().
	if(QDELETED(src))
		return
	var/mixcolor = mix_color_from_reagents(chemholder.reagents.reagent_list)
	if(holder)
		location = get_turf(holder)
	var/obj/effect/particle_effect/smoke/chem/S = new effect_type(location)

	if(chemholder.reagents.total_volume > 1) // can't split 1 very well
		chemholder.reagents.copy_to(S, chemholder.reagents.total_volume)

	if(mixcolor)
		S.add_atom_colour(mixcolor, FIXED_COLOUR_PRIORITY) // give the smoke color, if it has any to begin with
	S.amount = amount
	if(S.amount)
		S.spread_smoke() //calling process right now so the smoke immediately attacks mobs.

	// Химия отдана рождённому дыму, chemholder больше не нужен ни на что. Уборка здесь, а
	// не на совести вызывающего: см. autocleanup у типа.
	if(autocleanup)
		qdel(src)


/////////////////////////////////////////////
// Transparent smoke
/////////////////////////////////////////////

//Same as the base type, but the smoke produced is not opaque
/datum/effect_system/smoke_spread/transparent
	effect_type = /obj/effect/particle_effect/smoke/transparent

/datum/effect_system/smoke_spread/transparent/small
	amount = 1

/obj/effect/particle_effect/smoke/transparent
	opaque = FALSE

/proc/do_smoke(range=0, location=null, smoke_type=/obj/effect/particle_effect/smoke)
	var/datum/effect_system/smoke_spread/smoke = new
	smoke.effect_type = smoke_type
	smoke.set_up(range, location)
	smoke.start()

//Сигертный дым
/obj/effect/particle_effect/smoke/cigsmoke
	lifetime = 4
	alpha = 48

/datum/effect_system/smoke_spread/cigsmoke
	effect_type = /obj/effect/particle_effect/smoke/cigsmoke
